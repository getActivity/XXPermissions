plugins {
    alias(libs.plugins.agp.app)
}
apply from : '../common.gradle'

android {
    namespace = "com.hjq.permissions.demo"

    defaultConfig {
        // 最低安装版本
        minSdk = 21
        proguardFiles += "proguard-rules.pro"
    }

    // Apk 签名的那些事：https://www.jianshu.com/p/a1f8e5896aa2
    signingConfigs {
        config {
            storeFile file(StoreFile)
            storePassword StorePassword
            keyAlias KeyAlias
            keyPassword KeyPassword
        }
    }

    buildTypes {
        configureEach {
            signingConfig = signingConfigs.config
        }
        release {
            minifyEnabled = true
            shrinkResources = true
        }
    }

    applicationVariants.configureEach { variant ->
        // apk 输出文件名配置
        variant.outputs.configureEach { output ->
            outputFileName = rootProject.getName() + '.apk'
        }
    }
}

dependencies {
    // 依赖 libs 目录下所有的 jar 和 aar 包
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')

    implementation project(':library')

    // 谷歌兼容库：https://developer.android.google.cn/jetpack/androidx/releases/appcompat?hl=zh-cn
    implementation(libs.androidx.appcompat)

    // 吐司框架：https://github.com/getActivity/Toaster
    implementation(libs.getactivity.toaster)

    // 内存泄漏检测：https://github.com/square/leakcanary
    debugImplementation(libs.leakcanary.android)
}

configurations.implementation {
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}
