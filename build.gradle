allprojects {
    // 读取 local.properties 文件配置
    def properties = new Properties()
    def localPropertiesFile = rootProject.file("local.properties")
    if (localPropertiesFile.exists()) {
        localPropertiesFile.withInputStream { inputStream ->
            properties.load(inputStream)
        }
    }

    String buildDirPath = properties.getProperty("build.dir")
    if (buildDirPath != null && buildDirPath != "") {
        // 将构建文件统一输出到指定的目录下
        setBuildDir(new File(buildDirPath, rootProject.name + "/build/${path.replaceAll(':', '/')}"))
    } else {
        // 将构建文件统一输出到项目根目录下的 build 文件夹
        setBuildDir(new File(rootDir, "build/${path.replaceAll(':', '/')}"))
    }
}

ext {
    POM_GROUP = "com.github.getActivity"
    POM_DESCRIPTION = "Android 权限请求框架，已适配 Android 15"
    POM_URL = "https://github.com/getActivity/XXPermissions"
    POM_SCM = "scm:git@github.com:getActivity/XXPermissions"
    POM_LICENSE_NAME = "Apache-2.0"
    POM_LICENSE_URL = "https://github.com/getActivity/XXPermissions/blob/master/LICENSE"
    POM_DEVELOPER_NAME = "getActivity"
    POM_DEVELOPER_URL = "https://github.com/getActivity"
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}
